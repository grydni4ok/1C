## СТРУКТУРА БАЗЫ.
**Справочники.**
У справочника ФизическиеЛица добавим реквизиты ДатаПриемаНаРаботу (дата), ТрудовойСтажДоПриемаНаРаботуВДнях (число 6 0).
Создаем справочник ГрафикРаботы.

Документ **НачисленияЗарплаты** 
  - табличная часть ОсновныеНачисления (для зп) - Сотрудник(СправочникСсылка.ФизическиеЛица), ВидРасчета (ПланВидовРасчетаСсылка.ОсновныеНачисления), ГрафикРаботы (СправочникСсылка.ГрафикРаботы), ДатаНачала (Дата), ДатаОкончания (Дата);
  - табличная часть ДополнительныеНачисления (для премий) - Сотрудник(СправочникСсылка.ФизическиеЛица), ВидРасчета (ПланВидовРасчетаСсылка.ДополнительныеНачисления)

**Планы видов расчета**
ОсновныеНачисления - Расчет - Использует период действия ГАЛКА.
ДополнительныеНачисления - Расчет - Зависимость от базы - Зависит по периоду действия. Базовые плана видов расчета: Основные начисления. Прочее - предопределенные - Премия - Базовые - Оклад ГАЛКА

**Регистры**
В ГрафикиРаботы добавить измерение ГрафикРаботы (СправочникСсылка.ГрафикРаботы) ОсновнойОтбор Галка.
Создаем ТрудовойСтаж. Измерения От (Число 3 0), До (Число 3 0); Ресурс ПроцентПремии (Число 3 0).

**Регистры расчета**

  ОсновныеНачисления

![ОсновныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/01%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%9E%D1%81%D0%BD%D0%BE%D0%B2%D0%BD%D1%8B%D0%B5%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 12 2 Неотрицательное)
  3. Реквизиты: Оклад (Число 10 2 Неотрицательное); ГрафикРаботы (СправочникСсылка.ГрафикРаботы), **Связь с графиком - ГрафикРаботы**; РабочихДней (Число 2 0); ОтработаноДней (Число 2 0)

ДополнительныеНачисления

![ДополнительныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/01%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%94%D0%BE%D0%BF%D0%BE%D0%BB%D0%BD%D0%B8%D1%82%D0%B5%D0%BB%D1%8C%D0%BD%D1%8B%D0%B5%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F%202.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 10 2 )
  3. Реквизиты: Оклад (Число 10 2 Неотрицательное); ПроцентПремии (Число 3 0)


## Проведение документа НачислениеЗарплаты
```
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПериодРегистрации = НачалоМесяца(Дата);
	
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрокаОсновныеНачисления.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		//Движение.Значение = ?;
		//Движение.Оклад = ?;
		Движение.ГрафикРаботы = ТекСтрокаОсновныеНачисления.ГрафикРаботы;
	КонецЦикла;
	
	Движения.Записать();	

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
		|	ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисленияДанныеГрафика.Значение КАК Значение,
		|	ОсновныеНачисленияДанныеГрафика.Оклад КАК Оклад,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ГрафикРаботы, 0) КАК ГрафикРаботы,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК ЗначениеПериодДействия,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия
		|ПОМЕСТИТЬ втДанныеГрафика
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	втДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
		|	втДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец,
		|	втДанныеГрафика.Сотрудник КАК Сотрудник,
		|	втДанныеГрафика.Значение КАК Значение,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад,
		|	втДанныеГрафика.ГрафикРаботы КАК ГрафикРаботы,
		|	втДанныеГрафика.ЗначениеПериодДействия КАК ЗначениеПериодДействия,
		|	втДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия
		|ИЗ
		|	втДанныеГрафика КАК втДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&ПериодРегистрации,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						Т.Сотрудник
		|					ИЗ
		|						втДанныеГрафика КАК Т)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать(); 
	
	Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
		
		Выборка.Сбросить();
		ТекЗанчение = Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
		Движение.Оклад = Выборка.Оклад;
		Движение.РабочихДней = Выборка.ЗначениеПериодДействия;
		Движение.ОтработаноДней = Выборка.ЗначениеФактическийПериодДействия;
		Движение.Значение = ?(Выборка.ЗначениеПериодДействия = 0, 0, Выборка.ЗначениеФактическийПериодДействия / Выборка.ЗначениеПериодДействия * Выборка.Оклад);
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(, Истина); 	

	//Дополительные начисления	
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаДополнительныеНачисления Из ДополнительныеНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаДополнительныеНачисления.ВидРасчета;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.БазовыйПериодНачало = ПериодРегистрации;
		Движение.БазовыйПериодКонец = КонецМесяца(ПериодРегистрации);
		Движение.Сотрудник = ТекСтрокаДополнительныеНачисления.Сотрудник;
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.Значение КАК Значение,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.Оклад КАК Оклад,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.ЗначениеБаза КАК ЗначениеБаза,
		|	(РАЗНОСТЬДАТ(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ДатаПриемаНаРаботу, &ПериодРегистрации, ДЕНЬ) + ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ТрудовойСтажДоПриемаНаРаботу, 0)) / 365 КАК СтажВГодах
		|ПОМЕСТИТЬ втСотрудники
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК ДополнительныеНачисленияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСотрудники.НомерСтроки КАК НомерСтроки,
		|	втСотрудники.Сотрудник КАК Сотрудник,
		|	втСотрудники.ЗначениеБаза КАК ЗначениеБаза,
		|	втСотрудники.СтажВГодах КАК СтажВГодах,
		|	ТрудовойСтаж.ПроцентПремии КАК ПроцентПремии
		|ИЗ
		|	втСотрудники КАК втСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТрудовойСтаж КАК ТрудовойСтаж
		|		ПО втСотрудники.СтажВГодах >= ТрудовойСтаж.От
		|			И втСотрудники.СтажВГодах < ТрудовойСтаж.До";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать(); 
	
	Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			Движение.Значение = Выборка.ЗначениеБаза * Выборка.ПроцентПремии / 100;
			Движение.Оклад = Выборка.ЗначениеБаза;
			Движение.ПроцентПремии = Выборка.ПроцентПремии;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать();

КонецПроцедуры
```
