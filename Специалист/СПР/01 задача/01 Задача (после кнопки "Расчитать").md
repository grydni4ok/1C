## Структура базы

Заполняем уже созданными полями из Регистров расчета документ **НачислениеЗарплаты**, так же создаем форму и кнопку рассчитать 

![ОсновныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/01%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D0%B5%D0%97%D0%B0%D1%80%D0%BF%D0%BB%D0%B0%D1%82%D1%8B.jpg)

## Код кнопок и функций

Листинг кнопки **"Рассчитать"**
```
&НаКлиенте
Процедура Рассчитать(Команда)
	РассчитатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура РассчитатьНаСервере()
	
	ДанныеДляНачислений = Новый Структура("ПериодРегистрации", НачалоМесяца(Объект.Дата));
	
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	ДокОбъект.ЗаписьЗначенийВРегистры(ДанныеДляНачислений);
	ДокОбъект.РасчетРезультатаНачислений(ДанныеДляНачислений);
	
	ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
	
КонецПроцедуры
```
**ЗаписьЗначенийВРегистры** сюда мы пренестли код, который все поля таблиц ОсновныеНАчисления и ДополнительныеНачисления записывает в Регистры Расчета

```
Процедура ЗаписьЗначенийВРегистры(ДанныеДляНачислений) Экспорт
	
	ПериодРегистрации = ДанныеДляНачислений.ПериодРегистрации;
	
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрокаОсновныеНачисления.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		Движение.Значение = ТекСтрокаОсновныеНачисления.Результат;
		Движение.Оклад = ТекСтрокаОсновныеНачисления.Оклад;
		Движение.РабочихДней = ТекСтрокаОсновныеНачисления.РабочихДней;
		Движение.ОтработаноДней = ТекСтрокаОсновныеНачисления.ОтработаноДней;
		Движение.ГрафикРаботы = ТекСтрокаОсновныеНачисления.ГрафикРаботы;
	КонецЦикла;
	
	Движения.Записать();
	
	//Дополительные начисления	
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаДополнительныеНачисления Из ДополнительныеНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаДополнительныеНачисления.ВидРасчета;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.БазовыйПериодНачало = ПериодРегистрации;
		Движение.БазовыйПериодКонец = КонецМесяца(ПериодРегистрации);
		Движение.Сотрудник = ТекСтрокаДополнительныеНачисления.Сотрудник;
		Движение.Значение = ТекСтрокаДополнительныеНачисления.Результат;
		Движение.Оклад = ТекСтрокаДополнительныеНачисления.Оклад;
		Движение.ПроцентПремии = ТекСтрокаДополнительныеНачисления.ПроцентПремии;
		Движение.ТрудовойСтаж = ТекСтрокаДополнительныеНачисления.ТрудовойСтаж;
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать();
	
КонецПроцедуры
```

По этому в процедуре **ОбработкаПроведения** мы оставляем только процедуру ЗаписьЗначенийВРегистры

```
Процедура ОбработкаПроведения(Отказ, Режим)
	ДанныеДляНачислений = Новый Структура("ПериодРегистрации", НачалоМесяца(Дата));
	ЗаписьЗначенийВРегистры(ДанныеДляНачислений);
КонецПроцедуры
```

Процедура **РасчетРезультатаНачислений** используется только в кнопке рассчитать, по этому при расчете:
  1. **ОсновныхНАчислений** мы производим **запись и в Регистры расчета ОсновныеНАчисления в таблицу ОсновныеНАчисления** (записи в регистр все также нужны, потому что по ним будут произведены расчеты для дополнительных начислений)

```
Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
		
	Выборка.Сбросить();
	ТекЗанчение = Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
	Движение.Оклад = Выборка.Оклад;
	Движение.РабочихДней = Выборка.ЗначениеПериодДействия;
	Движение.ОтработаноДней = Выборка.ЗначениеФактическийПериодДействия;
	Движение.Значение = ?(Выборка.ЗначениеПериодДействия = 0, 0, Выборка.ЗначениеФактическийПериодДействия / Выборка.ЗначениеПериодДействия * Выборка.Оклад);

	СтрокаТаблицы = ОсновныеНачисления[Движение.НомерСтроки - 1];
	СтрокаТаблицы.Результат = Движение.Значение;
	СтрокаТаблицы.Оклад = Движение.Оклад;
	СтрокаТаблицы.РабочихДней = Движение.РабочихДней;
	СтрокаТаблицы.ОтработаноДней = Движение.ОтработаноДней;
КонецЦикла;
```

  2. **ДополнительныхНАчислений** нам вообще не нужны записи в регистр, мы сразу **записываем таблицу Дополнительные начисления**

```
Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
	Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
		СтрокаТаблицы = ДополнительныеНачисления[Движение.НомерСтроки - 1];
		СтрокаТаблицы.Результат = Выборка.ЗначениеБаза * Выборка.ПроцентПремии / 100;
		СтрокаТаблицы.Оклад = Выборка.ЗначениеБаза;
		СтрокаТаблицы.ПроцентПремии = Выборка.ПроцентПремии;
		СтрокаТаблицы.ТрудовойСтаж = Выборка.СтажВГодах;
	КонецЕсли;
	Выборка.Сбросить();
КонецЦикла;
```

  3. Очищаем записанные в Ригистры расчета данные

```
Движения.ОсновныеНачисления.Очистить();
Движения.ОсновныеНачисления.Записать();

Движения.ДополнительныеНачисления.Очистить();
Движения.ДополнительныеНачисления.Записать();

```

Листинг **РасчетРезультатаНачислений**

```
Процедура РасчетРезультатаНачислений(ДанныеДляНачислений) Экспорт
	
	ПериодРегистрации = ДанныеДляНачислений.ПериодРегистрации;

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
		|	ОсновныеНачисленияДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисленияДанныеГрафика.Значение КАК Значение,
		|	ОсновныеНачисленияДанныеГрафика.Оклад КАК Оклад,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ГрафикРаботы, 0) КАК ГрафикРаботы,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеПериодДействия, 0) КАК ЗначениеПериодДействия,
		|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия
		|ПОМЕСТИТЬ втДанныеГрафика
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Ссылка) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	втДанныеГрафика.ПериодДействияНачало КАК ПериодДействияНачало,
		|	втДанныеГрафика.ПериодДействияКонец КАК ПериодДействияКонец,
		|	втДанныеГрафика.Сотрудник КАК Сотрудник,
		|	втДанныеГрафика.Значение КАК Значение,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад,
		|	втДанныеГрафика.ГрафикРаботы КАК ГрафикРаботы,
		|	втДанныеГрафика.ЗначениеПериодДействия КАК ЗначениеПериодДействия,
		|	втДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия
		|ИЗ
		|	втДанныеГрафика КАК втДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&ПериодРегистрации,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						Т.Сотрудник
		|					ИЗ
		|						втДанныеГрафика КАК Т)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО втДанныеГрафика.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать(); 
	
	Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
		
		Выборка.Сбросить();
		ТекЗанчение = Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
		Движение.Оклад = Выборка.Оклад;
		Движение.РабочихДней = Выборка.ЗначениеПериодДействия;
		Движение.ОтработаноДней = Выборка.ЗначениеФактическийПериодДействия;
		Движение.Значение = ?(Выборка.ЗначениеПериодДействия = 0, 0, Выборка.ЗначениеФактическийПериодДействия / Выборка.ЗначениеПериодДействия * Выборка.Оклад);
		
		СтрокаТаблицы = ОсновныеНачисления[Движение.НомерСтроки - 1];
		СтрокаТаблицы.Результат = Движение.Значение;
		СтрокаТаблицы.Оклад = Движение.Оклад;
		СтрокаТаблицы.РабочихДней = Движение.РабочихДней;
		СтрокаТаблицы.ОтработаноДней = Движение.ОтработаноДней;
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(, Истина);	
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник КАК Сотрудник,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.Значение КАК Значение,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.Оклад КАК Оклад,
		|	ДополнительныеНачисленияБазаОсновныеНачисления.ЗначениеБаза КАК ЗначениеБаза,
		|	(РАЗНОСТЬДАТ(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ДатаПриемаНаРаботу, &ПериодРегистрации, ДЕНЬ) + ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник.ТрудовойСтажДоПриемаНаРаботу, 0)) / 365 КАК СтажВГодах
		|ПОМЕСТИТЬ втСотрудники
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК ДополнительныеНачисленияБазаОсновныеНачисления
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСотрудники.НомерСтроки КАК НомерСтроки,
		|	втСотрудники.Сотрудник КАК Сотрудник,
		|	втСотрудники.ЗначениеБаза КАК ЗначениеБаза,
		|	втСотрудники.СтажВГодах КАК СтажВГодах,
		|	ТрудовойСтаж.ПроцентПремии КАК ПроцентПремии
		|ИЗ
		|	втСотрудники КАК втСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТрудовойСтаж КАК ТрудовойСтаж
		|		ПО втСотрудники.СтажВГодах >= ТрудовойСтаж.От
		|			И втСотрудники.СтажВГодах < ТрудовойСтаж.До";
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	
	Запрос.УстановитьПараметр("Измерения", Измерения);
	Запрос.УстановитьПараметр("ПериодРегистрации", КонецМесяца(ПериодРегистрации));
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать(); 
	
	Для Каждого Движение Из Движения.ДополнительныеНачисления Цикл
		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			СтрокаТаблицы = ДополнительныеНачисления[Движение.НомерСтроки - 1];
			СтрокаТаблицы.Результат = Выборка.ЗначениеБаза * Выборка.ПроцентПремии / 100;
			СтрокаТаблицы.Оклад = Выборка.ЗначениеБаза;
			СтрокаТаблицы.ПроцентПремии = Выборка.ПроцентПремии;
			СтрокаТаблицы.ТрудовойСтаж = Выборка.СтажВГодах;
		КонецЕсли;
		Выборка.Сбросить();
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Очистить();
	Движения.ОсновныеНачисления.Записать();
	
	Движения.ДополнительныеНачисления.Очистить();
	Движения.ДополнительныеНачисления.Записать();
	
КонецПроцедуры
```



















