## СТРУКТУРА БАЗЫ.

**Справочники.**
Создаем справочник ГрафикиРаботы. Сразу у него создадим предопределнный элемент "Пятидневка", он нам понадобится в дальнейшем в коде.

Документ **НачисленияЗарплаты** 
  - табличная часть ОсновныеНачисления (Оклада и Больничных) - Сотрудник(СправочникСсылка.ФизическиеЛица), ГрафикРаботы (СправочникСсылка.ГрафикиРаботы), ВидРасчета (ПланВидовРасчетаСсылка.ОсновныеНачисления), ДатаНачала (Дата), ДатаОкончания (Дата);
  - табличная часть ДополнительныеНачисления (для компенсаций) - Сотрудник(СправочникСсылка.ФизическиеЛица), Подразделение (СправочникСсылка.Подразделения), ВидРасчета (ПланВидовРасчетаСсылка.ДополнительныеНачисления);
 
 **НаезженоЧасов**
  - табличная часть СписокСотрудников () - Сотрудник(СправочникСсылка.ФизическиеЛица), ВидРасчета (ПланВидовРасчетаСсылка.ОсновныеНачисления), СуммаПоощерения (Число 10 2);

**Обработки.**
В ЗаполнениеГрафика будет использовать и для заполнения сменного графика и для пядидневки. Для этого на форме создадим реквизит Сменный (Булево), и реквизит ГрафикРаботы (СправочникСсылка.ГрафикиРаботы)

![ЗаполнениеГрафика]()

```
Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни, Сменный, ГрафикРаботы) Экспорт 
	
	Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
	Набор.Отбор.ГрафикРаботы.Установить(ГрафикРаботы);
	ЧислоСекундВСутках = 86400;
	
	Дат = ДатаНачала;
	Счетчик = 1;
	Пока Дат <= ДатаОкончания Цикл
		Запись = Набор.Добавить();
		Запись.Дата = Дат;
		Запись.ГрафикРаботы = ГрафикРаботы;
		
		Если Сменный Тогда
			Если Счетчик = 1 Тогда
				Запись.Часов = 24;
				Запись.Дней = 1;
			Иначе	          
				Запись.Часов = 0;
				Запись.Дней = 0;
			КонецЕсли;
			Если Счетчик = 3 Тогда 
				Счетчик = 0;
			КонецЕсли;
			
			Счетчик = Счетчик + 1;
		Иначе 
			Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
				Запись.Часов = 0;
				Запись.Дней = 0;
			Иначе	          
				Запись.Часов = 8;
				Запись.Дней = 1;
			КонецЕсли;	
		КонецЕсли;
		
		Дат = Дат + ЧислоСекундВСутках;
	КонецЦикла; 
	Набор.Записать();
КонецПроцедуры

```

**Планы видов расчета.**
ОсновныеНачисления:
  1. Вкладка Расчет: Использует период действия ГАЛКА, Зависимсость от базы (Зависит по периоду действия), Базовые Планы Видов расчета (ОсновныеНачисления, ДополнительныеНачисления). 
  2. Предопределенные значения: Оклад - Вытесняющие (Больничный); Больничный - Базовые (Больничный, Оклад, Поощерение).
ДополнительныеНачисления:
  1. Вкладка Расчет: Использует период действия ГАЛКА(НЕТ), Зависимсость от базы (Не Зависит). 
  2. Предопределенные значения: Поощерение. 

**Регистры.**
В ГрафикиРаботы:
  1. Добваить Измерение ГрафикРаботы (СправочникСсылка.ГрафикиРаботы)
  2. Изменить сущесвующий ресурс на Часов (Число 5 2), и добавить новый Дней (Число 5 2). 

**Регистры расчета.**
Для Данной задачи необходимо создать регситр расчета **НаезженоЧасов**. Вид регистра - регистр остатков. Регистраторы: НаезженоЧасов, НаезженоЧасов.
Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 5 0)
 
**Регистры расчета.**

  ОсновныеНачисления

![ОсновныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/12%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%9E%D1%81%D0%BD%D0%BE%D0%B2%D0%BD%D1%8B%D0%B5%D0%9D%D0%90%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 12 2 )
  3. Реквизиты: ГрафикРаботы (СправочникСсылка.ГрафикиРаботы, **Связь с графиком - График работы**), Оклад (Число 10 2 Неотрицательное), ВсегоЧасов (Число 5 2), ОтработаноЧасов (Число 5 2), СуммаНачислений (Число 12 2 ), ВсегоДней (Число 5 2), ОтработаноДней (Число 5 2).

![ДополнительныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/12%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%94%D0%BE%D0%BF%D0%BE%D0%BB%D0%BD%D0%B8%D1%82%D0%B5%D0%BB%D1%8C%D0%BD%D1%8B%D0%B5%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 12 2 )

![СтруктураРегистров](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/12%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%A1%D1%82%D1%80%D1%83%D0%BA%D1%82%D1%83%D1%80%D0%B0%D0%A0%D0%B5%D0%B3%D1%81%D0%B8%D1%82%D1%80%D0%BE%D0%B2.jpg)

## Проведение документа НачислениеЗарплаты
```

Процедура ОбработкаПроведения(Отказ, Режим)
	ПериодРегистрации = НачалоМесяца(Дата);
	
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого Строка Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = Строка.ВидРасчета;
		Движение.ПериодДействияНачало = Строка.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.ГрафикРаботы = Строка.ГрафикРаботы;
		
		Если Строка.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Больничный Тогда
			Движение.БазовыйПериодНачало = ДобавитьМесяц(НачалоМесяца(Строка.ДатаНачала), -1);
			Движение.БазовыйПериодКонец = НачалоМесяца(Строка.ДатаОкончания) - 1;
			Движение.ГрафикРаботы = Справочники.ГрафикиРаботы.Пятидневка;
		КонецЕсли;		
	КонецЦикла;
	
	//Движение для стороно
	ТаблицаДополнение = Движения.ОсновныеНачисления.ПолучитьДополнение();
	Для Каждого Строка ИЗ ТаблицаДополнение Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Истина;
		ЗаполнитьЗначенияСвойств(Движение, Строка, "Сотрудник, ГрафикРаботы, Оклад, ВидРасчета");
		Движение.ПериодРегистрации = Строка.ПериодРегистрацииСторно;
		Движение.ПериодДействияНачало = Строка.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Строка.ПериодДействияКонецСторно;
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();
	
	//ДополнительныеНачисления
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого Строка Из ДополнительныеНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = Строка.ВидРасчета;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Значение = Строка.СуммаПоощерения;
	КонецЦикла;  
	
	Движения.ДополнительныеНачисления.Записать();
	
	//Запись основных начислений (результат по отработаным часам)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовФактическийПериодДействия, 0) КАК ЧасовФактическийПериодДействия,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЧасовПериодДействия, 0) КАК ВсегоЧасов
		|ПОМЕСТИТЬ втНачисления
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ВидРасчета В (&ВидРасчета)) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНачисления.НомерСтроки КАК НомерСтроки,
		|	втНачисления.Сотрудник КАК Сотрудник,
		|	втНачисления.ЧасовФактическийПериодДействия КАК ОтработаноЧасов,
		|	втНачисления.ВсегоЧасов КАК ВсегоЧасов,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0) КАК Оклад
		|ИЗ
		|	втНачисления КАК втНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&ПериодРегистрации,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						Т.Сотрудник
		|					ИЗ
		|						втНачисления КАК Т)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО втНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	ВидРасчета = Новый Массив;
	ВидРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Оклад);
	Запрос.УстановитьПараметр("ВидРасчета", ВидРасчета);
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();

	Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
		
		Если ВидРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
				
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			Движение.Значение = Выборка.Оклад / Выборка.ВсегоЧасов * Выборка.ОтработаноЧасов * ?(Движение.Сторно, -1, 1); 
			ЗаполнитьЗначенияСвойств(Движение, Выборка, "Оклад, ВсегоЧасов, ОтработаноЧасов");
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(, Истина);
	
	
	//Блок расчета больничхых
	//Для начала найдем список больничных и упорядочим их по дате, чтобы начеть расчет с самого раннего
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисления.ПериодДействия КАК ПериодДействия
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
		|ГДЕ
		|	ОсновныеНачисления.ВидРасчета В(&ВидРасчета)
		|	И ОсновныеНачисления.Регистратор = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	ПериодДействия";
	
	ВидРасчета = Новый Массив;
	ВидРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Больничный); 
	Запрос.УстановитьПараметр("ВидРасчета", ВидРасчета);
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	ВыборкаПД = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаПД.Следующий() Цикл
		
		//По больничным находим период, который нам необходима пересчиать и производим соответсвующие действия
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейФактическийПериодДействия, 0) КАК ОтработаноДней,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ДнейБазовыйПериод, 0) КАК ВсегоДней,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.ЗначениеБаза, 0) + ЕСТЬNULL(ОсновныеНачисленияБазаДополнительныеНачисления.ЗначениеБаза, 0) КАК СуммаНачислений
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ВидРасчета В (&ВидРасчета)
		|				И ПериодДействия = &ПериодДействия) КАК ОсновныеНачисленияДанныеГрафика
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Регистратор
		|					И ВидРасчета В (&ВидРасчета)
		|					И ПериодДействия = &ПериодДействия) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаДополнительныеНачисления(
		|				&Измерения,
		|				&Измерения,
		|				,
		|				Регистратор = &Регистратор
		|					И ВидРасчета В (&ВидРасчета)
		|					И ПериодДействия = &ПериодДействия) КАК ОсновныеНачисленияБазаДополнительныеНачисления
		|		ПО ОсновныеНачисленияДанныеГрафика.НомерСтроки = ОсновныеНачисленияБазаДополнительныеНачисления.НомерСтроки";
		
		ВидРасчета = Новый Массив;
		ВидРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Больничный);		
		Запрос.УстановитьПараметр("ВидРасчета", ВидРасчета);                 
		
		Измерения = Новый Массив;
		Измерения.Добавить("Сотрудник");
		Запрос.УстановитьПараметр("Измерения", Измерения);
		
		Запрос.УстановитьПараметр("ПериодДействия", ВыборкаПД.ПериодДействия);
		Запрос.УстановитьПараметр("Регистратор", Ссылка);
		
		РезультатЗапроса = Запрос.Выполнить(); 	
		Выборка = РезультатЗапроса.Выбрать();
		
		Для Каждого Движение ИЗ Движения.ОсновныеНачисления Цикл
			
			Если ВидРасчета.Найти(Движение.ВидРасчета) = Неопределено Тогда 
				Продолжить;
			КонецЕсли;
						
			Выборка.Сбросить();
			Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
				Движение.Значение = ?(Выборка.ВсегоДней = 0, 0, Выборка.ОтработаноДней * Выборка.СуммаНачислений / Выборка.ВсегоДней);
				ЗаполнитьЗначенияСвойств(Движение, Выборка, "СуммаНачислений, ВсегоДней, ОтработаноДней");
			КонецЕсли;
			
		КонецЦикла;		
		Движения.ОсновныеНачисления.Записать(, Истина);
			
	КонецЦикла;
КонецПроцедуры
```
