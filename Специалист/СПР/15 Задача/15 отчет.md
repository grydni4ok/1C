## Отчет

**ДиаграммаГанта**

Для диаграммы создаем форму (назначить форму основной). 
Создаем на форме реквизиты:
  1. ДиаграммаГанта (ДиаграммаГанта).
  2. Период (СтандартныйПериод)

Создаем кнопку "Сформировать"

![Отчет](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/15%20%D0%97%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%A4%D0%BE%D1%80%D0%BC%D0%B0.jpg)

**Код**

```
&НаКлиенте
Процедура Сформировать(Команда)
	СформироватьНаСервере();
КонецПроцедуры

&НаСервере
Процедура СформироватьНаСервере()  
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияФактическийПериодДействия.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисленияФактическийПериодДействия.ВидРасчета КАК ВидРасчета,
		|	ОсновныеНачисленияФактическийПериодДействия.ПериодДействияНачало КАК ПериодДействияНачало,
		|	ОсновныеНачисленияФактическийПериодДействия.ПериодДействияКонец КАК ПериодДействияКонец,
		|	ОсновныеНачисленияФактическийПериодДействия.Регистратор КАК Регистратор
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ФактическийПериодДействия КАК ОсновныеНачисленияФактическийПериодДействия
		|ГДЕ
		|	ОсновныеНачисленияФактическийПериодДействия.ПериодДействия МЕЖДУ &ФактическийПериодНачало И &ФактическийПериодКонец";
	
	Запрос.УстановитьПараметр("ФактическийПериодКонец", Период.ДатаОкончания);
	Запрос.УстановитьПараметр("ФактическийПериодНачало", Период.ДатаНачала);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Выборка = РезультатЗапроса.Выбрать(); 
	
	ДиаграммаГанта.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		Точка = ДиаграммаГанта.УстановитьТочку(Выборка.Сотрудник);
		Серия = ДиаграммаГанта.УстановитьСерию(Выборка.ВидРасчета);
		ЗначениеДГ = ДиаграммаГанта.ПолучитьЗначение(Точка, Серия);
		
		ИнтервалДГ = ЗначениеДГ.Добавить();
		ИнтервалДГ.Начало = Выборка.ПериодДействияНачало;
		ИнтервалДГ.Конец = Выборка.ПериодДействияКонец;
		
		ИнтервалДГ.Расшифровка = Выборка.Регистратор;
		
	КонецЦикла;
КонецПроцедуры
```














