## СТРУКТУРА БАЗЫ.
**Справочники.**
В этом задании доп спровочники никакие не нужны

Документ **НачисленияЗарплаты** 
  - табличная часть ОсновныеНачисления (для почасовой оплате и прогулов) - Сотрудник(СправочникСсылка.ФизическиеЛица), ВидРасчета (ПланВидовРасчетаСсылка.ОсновныеНачисления), ДатаНачала (Дата), ДатаОкончания (Дата);
  - табличная часть ДополнительныеНачисления (для компенсаций) - Сотрудник(СправочникСсылка.ФизическиеЛица), Подразделение (СправочникСсылка.Подразделения), ВидРасчета (ПланВидовРасчетаСсылка.ДополнительныеНачисления);
 
 **НаезженоЧасов**
  - табличная часть СписокСотрудников () - Сотрудник(СправочникСсылка.ФизическиеЛица), КоличествоЧасов (Число 3 0);

**Перечисления.**
Создаем перечиление АлгоритмыРасчета со знчениями ПочасоваяОплатаПоТарифу и НачислениеПремии. Они понадобятся для условия "Необходимо предоставить пользователю возможность самостоятельно создавать новые виды расчетов и привязывать их к существующим алгоритмам расчета". 

**Обработки.**
В ЗаполнениеГрафика будем заполнять сразу дни и часы для пятидневки.
```
Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни) Экспорт 
	
	Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
	ЧислоСекундВСутках = 86400;
	
	Дат = ДатаНачала;
	Пока Дат <= ДатаОкончания Цикл
		Запись = Набор.Добавить();
		Запись.Дата = Дат;
		Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
			Запись.ЗначениеВЧасах = 0;
			Запись.ЗначениеВДнях = 0;
		Иначе	          
			Запись.ЗначениеВЧасах = 8;
			Запись.ЗначениеВДнях = 1;
		КонецЕсли; 
		Дат = Дат + ЧислоСекундВСутках;
	КонецЦикла; 
	Набор.Записать();
КонецПроцедуры

```

**Планы видов расчета.**
ОсновныеНачисления:
  1. Вкладка Расчет: Использует период действия ГАЛКА, Зависимсость от базы (Не зависит). 
  2. Предопределенные значения: ОплатаПоЧасовомуТарифу - Вытесняющие (Прогул); Прогул.
ДополнительныеНачисления:
  1. Вкладка Расчет: Использует период действия ГАЛКА(НЕТ), Зависимсость от базы (Зависит по периоду действия), Базовые планы видо расчета (ОсновныеНачисления - ГАЛКА). 
  2. Предопределенные значения: Компенсация - Базовые (ОплатаПоЧасовомуТарифу). 

**Регистры.**
В ГрафикиРаботы Изменить сущесвующий ресурс на ЗначениеВЧасах (Число 5 2), и добавить новый ЗначениеВДнях (Число 5 2). 
В СведенияОСотрудниках изменим название существующего ресурса на ТарифнаяСтавка.
Создадим новый регистр КомпенсацияНаРемонт. Ресурс: Процент (Число 5 2).

**Регистры расчета.**
Для Данной задачи необходимо создать регситр расчета **НаезженоЧасов**. Вид регистра - регистр остатков. Регистраторы: НаезженоЧасов, НаезженоЧасов.
Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 5 0)
 
**Регистры расчета.**

  ОсновныеНачисления

![ОсновныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/04%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%9E%D1%81%D0%BD%D0%BE%D0%B2%D0%BD%D1%8B%D0%B5%D0%9D%D0%90%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F1.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица, **Базовое**), Подразделение (СправочникСсылка.Подразделения, **Базовое**)
  2. Ресурсы: Значение (Число 12 2 )
  3. Реквизиты: Тариф (Число 10 2 Неотрицательное); ОтработаноЧасов (Число 5 2), СменаГрафика (СправочникСсылка.СменыГрафика **Связь с графиком - СменаГрафика**)


![ДополнительныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/04%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%94%D0%BE%D0%BF%D0%BE%D0%BB%D0%BD%D0%B8%D1%82%D0%B5%D0%BB%D1%8C%D0%BD%D1%8B%D0%B5%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F1.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица, **Базовое**), Подразделение (СправочникСсылка.Подразделения, **Базовое**)
  2. Ресурсы: Значение (Число 12 2 )
  3. Реквизиты: ПроцентПремии (Число 5 2 Неотрицательное); БазаОкладСотрудников (Число 12 2)
Перерасчеты (ПерерасчетПремии):
  1. Измерения: Сотрудник (Измерение регистра - Сотрудник, Данные ведущих регситров - Сотрудник), Подразделение (Измерение регистра - Подразделение, Данные ведущих регситров - Подразделение)

![СтруктураРегистров](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/04%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%A1%D1%82%D1%80%D1%83%D1%82%D0%BA%D1%82%D1%83%D1%80%D0%B0%D0%A0%D0%B5%D0%B3%D0%B8%D1%81%D1%82%D1%80%D0%BE%D0%A0%D0%B0%D1%81%D1%87%D0%B5%D1%82%D0%B0.jpg)

## Проведение документа НачислениеЗарплаты
```
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПериодРегистрации = НачалоМесяца(Дата);

	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрокаОсновныеНачисления.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		Движение.Подразделение = ТекСтрокаОсновныеНачисления.Подразделение;
		//Движение.Значение = ?;
		Движение.СменаГрафика = ТекСтрокаОсновныеНачисления.СменаГрафика;
	КонецЦикла; 
	
	Движения.ОсновныеНачисления.Записать(); 
	
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого Строка ИЗ ДополнительныеНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = Строка.ВидРасчета;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.Подразделение = Строка.Подразделение;
		Движение.ПроцентПремии = Строка.ПроцентПремии;
		Движение.БазовыйПериодНачало = ДобавитьМесяц(ПериодРегистрации, -1);
		Движение.БазовыйПериодКонец = ПериодРегистрации - 1;
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать();
	
	//Запрос для установления приоритетов в видах расчета заданых пользователем, самый высокий приоритет будет идти первым
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисления.ВидРасчета.АлгоритмРасчета КАК ВидРасчетаАлгоритмРасчета,
		|	ОсновныеНачисления.ВидРасчета.Приоритет КАК ВидРасчетаПриоритет
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления КАК ОсновныеНачисления
		|ГДЕ
		|	ОсновныеНачисления.Регистратор = &Регистратор
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ДополнительныеНачисления.ВидРасчета.АлгоритмРасчета,
		|	ДополнительныеНачисления.ВидРасчета.Приоритет
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.Регистратор = &Регистратор
		|
		|УПОРЯДОЧИТЬ ПО
		|	ВидРасчетаПриоритет";
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	ВыборкаАлгоритмыРасчета = РезультатЗапроса.Выбрать();
	
	//Для алгоритмов расчета с типом ПочасоваяОплатаПоТарифу Мы расчитываем по одному алгоритму (основные начисления)
	//а для расчета с типом НачислениеПремии мы используем другой алгоритм (дополнительные начисления)
	Пока ВыборкаАлгоритмыРасчета.Следующий() Цикл
		Если ВыборкаАлгоритмыРасчета.ВидРасчетаАлгоритмРасчета = Перечисления.АлгоритмыРасчета.ПочасоваяОплатаПоТарифу Тогда 
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
			|	ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
			|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
			|	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
			|	ОсновныеНачисленияДанныеГрафика.ЗначениеФактическийПериодДействия КАК ЗначениеФактическийПериодДействия,
			|	ОсновныеНачисленияДанныеГрафика.СменаГрафика КАК СменаГрафика
			|ПОМЕСТИТЬ втОсновныеНачисления
			|ИЗ
			|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
			|			Регистратор = &Регистратор
			|				И ВидРасчета.АлгоритмРасчета = &АлгоритмРасчета) КАК ОсновныеНачисленияДанныеГрафика
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	СменаГрафика
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	втОсновныеНачисления.НомерСтроки КАК НомерСтроки,
			|	втОсновныеНачисления.ВидРасчета КАК ВидРасчета,
			|	втОсновныеНачисления.Сотрудник КАК Сотрудник,
			|	втОсновныеНачисления.Подразделение КАК Подразделение,
			|	втОсновныеНачисления.ЗначениеФактическийПериодДействия КАК ОтработаноЧасов,
			|	втОсновныеНачисления.СменаГрафика КАК СменаГрафика,
			|	ЕСТЬNULL(ТарифнаяСтавкаСрезПоследних.Тариф, 0) КАК Тариф
			|ИЗ
			|	втОсновныеНачисления КАК втОсновныеНачисления
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ТарифнаяСтавка.СрезПоследних(
			|				&ПериодРегистрации,
			|				СменаГрафика В
			|					(ВЫБРАТЬ
			|						Т.СменаГрафика
			|					ИЗ
			|						втОсновныеНАчисления КАК Т)) КАК ТарифнаяСтавкаСрезПоследних
			|		ПО втОсновныеНачисления.СменаГрафика = ТарифнаяСтавкаСрезПоследних.СменаГрафика";
			
			Запрос.УстановитьПараметр("АлгоритмРасчета", ВыборкаАлгоритмыРасчета.ВидРасчетаАлгоритмРасчета);
			Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
			Запрос.УстановитьПараметр("Регистратор", Ссылка);
			
			РезультатЗапроса = Запрос.Выполнить(); 
			Выборка = РезультатЗапроса.Выбрать();
			
			Для Каждого Движение ИЗ Движения.ОсновныеНачисления Цикл
				
				Выборка.Сбросить();
				Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
					Движение.Значение = Выборка.Тариф * Выборка.ОтработаноЧасов;
					Движение.Тариф = Выборка.Тариф;
					Движение.ОтработаноЧасов = Выборка.ОтработаноЧасов;
				КонецЕсли;
				
			КонецЦикла;
			
			Движения.ОсновныеНачисления.Записать(, Истина); 
			
		ИначеЕсли ВыборкаАлгоритмыРасчета.ВидРасчетаАлгоритмРасчета = Перечисления.АлгоритмыРасчета.НачислениеПремии Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки КАК НомерСтроки,
			|	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник КАК Сотрудник,
			|	СУММА(ДополнительныеНачисленияБазаОсновныеНачисления.ЗначениеБаза) КАК ЗначениеБаза,
			|	ДополнительныеНачисленияБазаОсновныеНачисления.ПроцентПремии КАК ПроцентПремии
			|ИЗ
			|	РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
			|			&Измерения,
			|			&Измерения,
			|			&Разрезы,
			|			Регистратор = &Регистратор
			|				И ВидРасчета.АлгоритмРасчета = &АлгоритмРасчета) КАК ДополнительныеНачисленияБазаОсновныеНачисления
			|ГДЕ
			|	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник <> ДополнительныеНачисленияБазаОсновныеНачисления.СотрудникРазрез
			|
			|СГРУППИРОВАТЬ ПО
			|	ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки,
			|	ДополнительныеНачисленияБазаОсновныеНачисления.Сотрудник,
			|	ДополнительныеНачисленияБазаОсновныеНачисления.ПроцентПремии";
			
			Запрос.УстановитьПараметр("АлгоритмРасчета", ВыборкаАлгоритмыРасчета.ВидРасчетаАлгоритмРасчета);
			Запрос.УстановитьПараметр("Регистратор", Ссылка);
			
			Измерения = Новый Массив;
			Измерения.Добавить("Подразделение");
			Запрос.УстановитьПараметр("Измерения", Измерения);
			
			Разрезы = Новый Массив;
			Разрезы.Добавить("Сотрудник");
			Запрос.УстановитьПараметр("Разрезы", Разрезы);
			
			РезультатЗапроса = Запрос.Выполнить();	
			Выборка = РезультатЗапроса.Выбрать();
			
			Для Каждого Движение ИЗ Движения.ДополнительныеНачисления Цикл
				
				Выборка.Сбросить();
				Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
					Движение.Значение = Выборка.ЗначениеБаза * Выборка.ПроцентПремии / 100;
					Движение.БазаОкладСотрудников = Выборка.ЗначениеБаза;
				КонецЕсли;
				
			КонецЦикла;
			
			Движения.ДополнительныеНачисления.Записать();
			
		КонецЕсли;

	КонецЦикла;
	
КонецПроцедуры
```
