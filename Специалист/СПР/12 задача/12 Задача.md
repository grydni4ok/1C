## СТРУКТУРА БАЗЫ.
**Константы.**
ШтрафЗаНеВыход (Число 5 0), НормаЧасов (Число 5 0)

**Справочники.**
В этом задании доп спровочники никакие не нужны

Документ **НачисленияЗарплаты** 
  - табличная часть ОсновныеНачисления (для почасовой оплате и прогулов) - Сотрудник(СправочникСсылка.ФизическиеЛица), ВидРасчета (ПланВидовРасчетаСсылка.ОсновныеНачисления), ДатаНачала (Дата), ДатаОкончания (Дата);
  - табличная часть ДополнительныеНачисления (для компенсаций) - Сотрудник(СправочникСсылка.ФизическиеЛица), Подразделение (СправочникСсылка.Подразделения), ВидРасчета (ПланВидовРасчетаСсылка.ДополнительныеНачисления);
 
 **НаезженоЧасов**
  - табличная часть СписокСотрудников () - Сотрудник(СправочникСсылка.ФизическиеЛица), КоличествоЧасов (Число 3 0);

**Перечисления.**
Создаем перечиление АлгоритмыРасчета со знчениями ПочасоваяОплатаПоТарифу и НачислениеПремии. Они понадобятся для условия "Необходимо предоставить пользователю возможность самостоятельно создавать новые виды расчетов и привязывать их к существующим алгоритмам расчета". 

**Обработки.**
В ЗаполнениеГрафика будем заполнять сразу дни и часы для пятидневки.
```
Процедура ЗаполнитьГрафик(ДатаНачала, ДатаОкончания, ВыходныеДни) Экспорт 
	
	Набор = РегистрыСведений.ГрафикиРаботы.СоздатьНаборЗаписей();
	ЧислоСекундВСутках = 86400;
	
	Дат = ДатаНачала;
	Пока Дат <= ДатаОкончания Цикл
		Запись = Набор.Добавить();
		Запись.Дата = Дат;
		Если Найти(ВыходныеДни, Строка(ДеньНедели(Дат))) Тогда
			Запись.ЗначениеВЧасах = 0;
			Запись.ЗначениеВДнях = 0;
		Иначе	          
			Запись.ЗначениеВЧасах = 8;
			Запись.ЗначениеВДнях = 1;
		КонецЕсли; 
		Дат = Дат + ЧислоСекундВСутках;
	КонецЦикла; 
	Набор.Записать();
КонецПроцедуры

```

**Планы видов расчета.**
ОсновныеНачисления:
  1. Вкладка Расчет: Использует период действия ГАЛКА, Зависимсость от базы (Не зависит). 
  2. Предопределенные значения: ОплатаПоЧасовомуТарифу - Вытесняющие (Прогул); Прогул.
ДополнительныеНачисления:
  1. Вкладка Расчет: Использует период действия ГАЛКА(НЕТ), Зависимсость от базы (Зависит по периоду действия), Базовые планы видо расчета (ОсновныеНачисления - ГАЛКА). 
  2. Предопределенные значения: Компенсация - Базовые (ОплатаПоЧасовомуТарифу). 

**Регистры.**
В ГрафикиРаботы Изменить сущесвующий ресурс на ЗначениеВЧасах (Число 5 2), и добавить новый ЗначениеВДнях (Число 5 2). 
В СведенияОСотрудниках изменим название существующего ресурса на ТарифнаяСтавка.
Создадим новый регистр КомпенсацияНаРемонт. Ресурс: Процент (Число 5 2).

**Регистры расчета.**
Для Данной задачи необходимо создать регситр расчета **НаезженоЧасов**. Вид регистра - регистр остатков. Регистраторы: НаезженоЧасов, НаезженоЧасов.
Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 5 0)
 
**Регистры расчета.**

  ОсновныеНачисления

![ОсновныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/12%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%9E%D1%81%D0%BD%D0%BE%D0%B2%D0%BD%D1%8B%D0%B5%D0%9D%D0%90%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 12 2 )
  3. Реквизиты: ТарифнаяСтавка (Число 10 2 Неотрицательное), ОтработаноЧасов (Число 5 2), ШтрафЗаНеВыход (Число 5 0), ОтработаноДней (Число 5 2).


![ДополнительныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/12%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%94%D0%BE%D0%BF%D0%BE%D0%BB%D0%BD%D0%B8%D1%82%D0%B5%D0%BB%D1%8C%D0%BD%D1%8B%D0%B5%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица)
  2. Ресурсы: Значение (Число 12 2 )
  3. Реквизиты: Процент (Число 5 2 Неотрицательное); НачисленияВРасчетномПериоде (Число 12 2), НаезженоЧасов (Число 5 0)

![СтруктураРегистров](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/12%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%A1%D1%82%D1%80%D1%83%D0%BA%D1%82%D1%83%D1%80%D0%B0%D0%A0%D0%B5%D0%B3%D1%81%D0%B8%D1%82%D1%80%D0%BE%D0%B2.jpg)

## Проведение документа НачислениеЗарплаты
```
Процедура ОбработкаПроведения(Отказ, Режим)
	ПериодРегистрации = НачалоМесяца(Дата);

	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого Строка Из ОсновныеНачисления Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = Строка.ВидРасчета;
		Движение.ПериодДействияНачало = Строка.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(Строка.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = Строка.Сотрудник;
		//Движение.Значение = ?;
	КонецЦикла;
                                                         
	Движения.ОсновныеНачисления.Записать(); 
	
	// регистр ДополнительныНачисления
	Движения.ДополнительныеНачисления.Записывать = Истина;
	Для Каждого Строка Из ДополнительныеНачисления Цикл
		Движение = Движения.ДополнительныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = Строка.ВидРасчета;
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = Строка.Сотрудник;
		Движение.БазовыйПериодНачало = ПериодРегистрации;
		Движение.БазовыйПериодКонец = КонецМесяца(ПериодРегистрации);
	КонецЦикла; 
	
	Движения.ДополнительныеНачисления.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеВЧасахФактическийПериодДействия, 0) КАК ЗначениеВЧасахФактическийПериодДействия,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеВДняхФактическийПериодДействия, 0) КАК ЗначениеВДняхФактическийПериодДействия
		|ПОМЕСТИТЬ втНачисления
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(
		|			Регистратор = &Регистратор
		|				И ВидРасчета В (&ВидРасчета)) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втНачисления.НомерСтроки КАК НомерСтроки,
		|	втНачисления.Сотрудник КАК Сотрудник,
		|	втНачисления.ЗначениеВЧасахФактическийПериодДействия КАК ОтработаноЧасов,
		|	втНачисления.ЗначениеВДняхФактическийПериодДействия КАК ОтработаноДней,
		|	ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.ТарифнаяСтавка, 0) КАК ТарифнаяСтавка
		|ИЗ
		|	втНачисления КАК втНачисления
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&ПериодРегистрации,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						Т.Сотрудник
		|					ИЗ
		|						втНачисления КАК Т)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО втНачисления.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник";
	
	ВидРасчета = Новый Массив;
	ВидРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу);
	ВидРасчета.Добавить(ПланыВидовРасчета.ОсновныеНачисления.Прогул);

	Запрос.УстановитьПараметр("ВидРасчета", ВидРасчета);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Выборка = РезультатЗапроса.Выбрать();
	
	ШтрафЗаНеВыход = Константы.ШтрафЗаНеВыход.Получить();
	Для Каждого Движение ИЗ Движения.ОсновныеНачисления Цикл
		
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			
			Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.ОплатаПоЧасовомуТарифу Тогда 			
				Движение.Значение = Выборка.ТарифнаяСтавка * Выборка.ОтработаноЧасов;
				ЗаполнитьЗначенияСвойств(Движение, Выборка, "ОтработаноЧасов, ТарифнаяСтавка"); 
			КонецЕсли;
			
			Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Прогул Тогда
				Движение.Значение = ШтрафЗаНеВыход * Выборка.ОтработаноДней;
				ЗаполнитьЗначенияСвойств(Движение, Выборка, "ОтработаноДней");
				Движение.ШтрафЗаНеВыход = ШтрафЗаНеВыход;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать(, Истина);  
	Движения.НаезженоЧасов.Записать(); 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ДополнительныеНачисления.НомерСтроки КАК НомерСтроки,
		|	ДополнительныеНачисления.Сотрудник КАК Сотрудник
		|ПОМЕСТИТЬ втСотрудники
		|ИЗ
		|	РегистрРасчета.ДополнительныеНачисления КАК ДополнительныеНачисления
		|ГДЕ
		|	ДополнительныеНачисления.Регистратор = &Регистратор
		|	И ДополнительныеНачисления.ВидРасчета В(&ВидРасчета)
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСотрудники.НомерСтроки КАК НомерСтроки,
		|	НаезженоЧасовОстатки.Сотрудник КАК Сотрудник,
		|	ЕСТЬNULL(ДополнительныеНачисленияБазаОсновныеНачисления.ЗначениеБаза, 0) КАК НачисленияВРасчетномПериоде,
		|	ЕСТЬNULL(НаезженоЧасовОстатки.ЗначениеОстаток, 0) КАК НаезженоЧасов,
		|	КомпенсацияНаРемонтСрезПоследних.Процент КАК Процент
		|ИЗ
		|	втСотрудники КАК втСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.НаезженоЧасов.Остатки(
		|				&КонецПериодаРегистрации,
		|				Сотрудник В
		|					(ВЫБРАТЬ
		|						Т.Сотрудник
		|					ИЗ
		|						втСотрудники КАК Т)) КАК НаезженоЧасовОстатки
		|		ПО втСотрудники.Сотрудник = НаезженоЧасовОстатки.Сотрудник
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ДополнительныеНачисления.БазаОсновныеНачисления(
		|				&Измерение,
		|				&Измерение,
		|				,
		|				Регистратор = &Регистратор
		|					И ВидРасчета В (&ВидРасчета)) КАК ДополнительныеНачисленияБазаОсновныеНачисления
		|		ПО втСотрудники.НомерСтроки = ДополнительныеНачисленияБазаОсновныеНачисления.НомерСтроки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КомпенсацияНаРемонт.СрезПоследних(&ПериодРегистрации, ) КАК КомпенсацияНаРемонтСрезПоследних
		|		ПО (ИСТИНА)
		|ГДЕ
		|	НаезженоЧасовОстатки.ЗначениеОстаток >= &НормаЧасов";
	
	ВидРасчета = Новый Массив;
	ВидРасчета.Добавить(ПланыВидовРасчета.ДополнительныеНачисления.Компенсация);
	Запрос.УстановитьПараметр("ВидРасчета", ВидРасчета);
	
	Измерение = Новый Массив;
	Измерение.Добавить("Сотрудник");
	Запрос.УстановитьПараметр("Измерение", Измерение);
	
	Запрос.УстановитьПараметр("КонецПериодаРегистрации", КонецМесяца(ПериодРегистрации));
	НормаЧасов = Константы.НормаЧасов.Получить();
	Запрос.УстановитьПараметр("НормаЧасов", НормаЧасов);
	
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	
	РезультатЗапроса = Запрос.Выполнить(); 
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого Движение ИЗ Движения.ДополнительныеНачисления Цикл
		
		Выборка.Сбросить();
		Если Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки") Тогда
			Движение.Значение = Выборка.НачисленияВРасчетномПериоде * Выборка.Процент / 100;
			ЗаполнитьЗначенияСвойств(Движение, Выборка, "Процент, НачисленияВРасчетномПериоде, НаезженоЧасов");
			
			//регистр НаезженоЧасов 
			Движение = Движения.НаезженоЧасов.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = КонецМесяца(ПериодРегистрации);
			Движение.Сотрудник = Выборка.Сотрудник;
			Движение.Значение = НормаЧасов;
			
			Движения.НаезженоЧасов.Записывать = Истина;
			
		КонецЕсли;
			
	КонецЦикла;
	
	Движения.ДополнительныеНачисления.Записать();

КонецПроцедуры
```
