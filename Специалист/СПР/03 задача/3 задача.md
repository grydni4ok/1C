## СТРУКТУРА БАЗЫ.
**Справочники.**
Создаем справочник ГрафикРаботы.

Документ **НачисленияЗарплаты** 
  - табличная часть ОсновныеНачисления (для зп и для командировок) - Сотрудник(СправочникСсылка.ФизическиеЛица), ГрафикРаботы (СправочникСсылка.ГрафикРаботы), Подразделение (СправочникСсылка.Подразделения), ВидРасчета (ПланВидовРасчетаСсылка.ОсновныеНачисления),  ДатаНачала (Дата), ДатаОкончания (Дата);

**Планы видов расчета**
ОсновныеНачисления:
  1. Вкладка Расчет: Использует период действия ГАЛКА, Зависимсость от базы (Зависит по периоду действия), База (ОсновныеНАчисления)
  2. Предопределенные значения - Оклад (Вытесняющие - Командировка), Командировка (Базовые - Оклад)

**Регистры**
В ГрафикиРаботы добавить измерение ГрафикРаботы (СправочникСсылка.ГрафикРаботы) ОсновнойОтбор Галка, Необходимо 2 ресурса: ЗначениеВДнях (Число 5 2), ЗначениеВЧасах (Число 5 2)

**Регистры расчета**

  ОсновныеНачисления

![ОсновныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/03%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%9E%D1%81%D0%BD%D0%BE%D0%B2%D0%BD%D1%8B%D0%B5%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F1.jpg)

Данные:
  1. Измерения: Сотрудник (СправочникСсылка.ФизическиеЛица), Подразделение (СправочникСсылка.Подразделения)
  2. Ресурсы: Значение (Число 12 2 ), ОтработаноЧасов (Число 5 2)
  3. Реквизиты: Оклад (Число 10 2 Неотрицательное); ВсегоДней (Число 5 2); ОтработаноДней (Число 5 2), ГрафикРаботы (СправочникСсылка.ГрафикРаботы **Связь с графиком - ГрафикРаботы**), НачисленияЗаБазовыйПериод (Число 12 2), ОтработаноЧасовВБазовомПериоде (Число 5 2)


![ОсновныеНачисления](https://raw.githubusercontent.com/grydni4ok/1C/main/%D0%A1%D0%BF%D0%B5%D1%86%D0%B8%D0%B0%D0%BB%D0%B8%D1%81%D1%82/%D0%A1%D0%9F%D0%A0/03%20%D0%B7%D0%B0%D0%B4%D0%B0%D1%87%D0%B0/%D0%9E%D1%81%D0%BD%D0%BE%D0%B2%D0%BD%D1%8B%D0%B5%D0%9D%D0%B0%D1%87%D0%B8%D1%81%D0%BB%D0%B5%D0%BD%D0%B8%D1%8F2.jpg)


## Проведение документа НачислениеЗарплаты
```
Процедура ОбработкаПроведения(Отказ, Режим)
	
	ПериодРегистрации = НачалоМесяца(Дата);
	
	// регистр ОсновныеНачисления
	Движения.ОсновныеНачисления.Записывать = Истина;
	Для Каждого ТекСтрокаОсновныеНачисления Из ОсновныеНачисления Цикл
		//записи для формирований данных о зп и базовых данных командировок
		Движение = Движения.ОсновныеНачисления.Добавить();
		Движение.Сторно = Ложь;
		Движение.ВидРасчета = ТекСтрокаОсновныеНачисления.ВидРасчета;
		Движение.ПериодДействияНачало = ТекСтрокаОсновныеНачисления.ДатаНачала;
		Движение.ПериодДействияКонец = КонецДня(ТекСтрокаОсновныеНачисления.ДатаОкончания);
		Движение.ПериодРегистрации = ПериодРегистрации;
		Движение.Сотрудник = ТекСтрокаОсновныеНачисления.Сотрудник;
		Движение.Подразделение = ТекСтрокаОсновныеНачисления.Подразделение;
		Движение.ГрафикРаботы = ТекСтрокаОсновныеНачисления.ГрафикРаботы;
		//Движение.Значение = ?;
		//Движение.Оклад = ?;
		
		//для командировки записываем шестидневку и устанавливаем базу для начислений
		Если ТекСтрокаОсновныеНачисления.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Командировка Тогда
			Движение.ГрафикРаботы = Справочники.ГрафикРаботы.Шестидневка;
			Движение.БазовыйПериодНачало = ДобавитьМесяц(НачалоМесяца(ТекСтрокаОсновныеНачисления.ДатаНачала), -2);
			Движение.БазовыйПериодКонец = НачалоМесяца(ТекСтрокаОсновныеНачисления.ДатаНачала) - 1;
		КонецЕсли;
		
	КонецЦикла;  
	
	//для сторно записей нужны отдельные записи
	ТаблицаДополнение = Движения.ОсновныеНачисления.ПолучитьДополнение();
	Для Каждого Строка Из ТаблицаДополнение Цикл
		Движение = Движения.ОсновныеНачисления.Добавить();
		ЗаполнитьЗначенияСвойств(Движение, Строка, "Сотрудник, Подразделение, ВидРасчета, ГрафикРаботы, Оклад");
		Движение.ПериодДействияНачало = Строка.ПериодДействияНачалоСторно;
		Движение.ПериодДействияКонец = Строка.ПериодДействияКонецСторно;
		Движение.ПериодРегистрации = Строка.ПериодРегистрацииСторно;
		Движение.Сторно = Истина;
	КонецЦикла;
	
	Движения.ОсновныеНачисления.Записать();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОсновныеНачисленияДанныеГрафика.НомерСтроки КАК НомерСтроки,
		|	ОсновныеНачисленияДанныеГрафика.ВидРасчета КАК ВидРасчета,
		|	ОсновныеНачисленияДанныеГрафика.Сотрудник КАК Сотрудник,
		|	ОсновныеНачисленияДанныеГрафика.Подразделение КАК Подразделение,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеВДняхПериодДействия, 0) КАК ВсегоДней,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеВДняхФактическийПериодДействия, 0) КАК ОтработаноДней,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеВЧасахПериодДействия, 0) КАК ВсегоЧасов,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.ЗначениеВЧасахФактическийПериодДействия, 0) КАК ОтработаноЧасов,
		|	ЕСТЬNULL(ОсновныеНачисленияДанныеГрафика.Оклад, 0) КАК Оклад,
		|	ОсновныеНачисленияДанныеГрафика.Сторно КАК Сторно
		|ПОМЕСТИТЬ втСотрудники
		|ИЗ
		|	РегистрРасчета.ОсновныеНачисления.ДанныеГрафика(Регистратор = &Регистратор) КАК ОсновныеНачисленияДанныеГрафика
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Сотрудник,
		|	Подразделение,
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втСотрудники.НомерСтроки КАК НомерСтроки,
		|	втСотрудники.ВидРасчета КАК ВидРасчета,
		|	втСотрудники.Сотрудник КАК Сотрудник,
		|	втСотрудники.Подразделение КАК Подразделение,
		|	втСотрудники.ВсегоДней КАК ВсегоДней,
		|	втСотрудники.ОтработаноДней КАК ОтработаноДней,
		|	втСотрудники.ВсегоЧасов КАК ВсегоЧасов,
		|	втСотрудники.ОтработаноЧасов КАК ОтработаноЧасов,
		|	ВЫБОР
		|		КОГДА втСотрудники.Сторно
		|			ТОГДА втСотрудники.Оклад
		|		ИНАЧЕ ЕСТЬNULL(СведенияОСотрудникахСрезПоследних.Оклад, 0)
		|	КОНЕЦ КАК Оклад,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.ЗначениеБаза, 0) КАК НачисленияЗаБазовыйПериод,
		|	ЕСТЬNULL(ОсновныеНачисленияБазаОсновныеНачисления.ОтработаноЧасовБаза, 0) КАК ОтработаноЧасовЗаБазовыйПериод,
		|	втСотрудники.Сторно КАК Сторно
		|ИЗ
		|	втСотрудники КАК втСотрудники
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияОСотрудниках.СрезПоследних(
		|				&ПериодРегистрации,
		|				(Сотрудник, Подразделение) В
		|					(ВЫБРАТЬ
		|						Т.Сотрудник,
		|						Т.Подразделение
		|					ИЗ
		|						втСотрудники КАК Т)) КАК СведенияОСотрудникахСрезПоследних
		|		ПО втСотрудники.Сотрудник = СведенияОСотрудникахСрезПоследних.Сотрудник
		|			И втСотрудники.Подразделение = СведенияОСотрудникахСрезПоследних.Подразделение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрРасчета.ОсновныеНачисления.БазаОсновныеНачисления(&Измерения, &Измерения, , Регистратор = &Регистратор) КАК ОсновныеНачисленияБазаОсновныеНачисления
		|		ПО втСотрудники.НомерСтроки = ОсновныеНачисленияБазаОсновныеНачисления.НомерСтроки";
	
	Запрос.УстановитьПараметр("ПериодРегистрации", ПериодРегистрации);
	Запрос.УстановитьПараметр("Регистратор", Ссылка);
	
	Измерения = Новый Массив;
	Измерения.Добавить("Сотрудник");
	Измерения.Добавить("Подразделение");
	Запрос.УстановитьПараметр("Измерения", Измерения);
	
	РезультатЗапроса = Запрос.Выполнить();	
	Выборка = РезультатЗапроса.Выбрать();
	
	Для Каждого Движение Из Движения.ОсновныеНачисления Цикл
		Выборка.Сбросить();
		Выборка.НайтиСледующий(Движение.НомерСтроки, "НомерСтроки");
		ЗаполнитьЗначенияСвойств(Движение, Выборка, "Оклад, ВсегоДней, ОтработаноДней, ОтработаноЧасов");
		Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Оклад Тогда
			Движение.Значение = ?(Выборка.ВсегоДней = 0, 0, Выборка.ОтработаноДней / Выборка.ВсегоДней * Выборка.Оклад);
		КонецЕсли;
		
		Если Движение.ВидРасчета = ПланыВидовРасчета.ОсновныеНачисления.Командировка Тогда
			Движение.Значение = ?(Выборка.ОтработаноЧасовЗаБазовыйПериод = 0, 0,
								  Выборка.НачисленияЗаБазовыйПериод / Выборка.ОтработаноЧасовЗаБазовыйПериод * Выборка.ОтработаноЧасов);
			Движение.НачисленияЗаБазовыйПериод = Выборка.НачисленияЗаБазовыйПериод;
			Движение.ОтработаноЧасовВБазовомПериоде = Выборка.ОтработаноЧасовЗаБазовыйПериод;
		КонецЕсли;
		
		Если Выборка.Сторно Тогда
			Движение.Значение = Движение.Значение * -1;
			Движение.ОтработаноЧасов = Движение.ОтработаноЧасов * -1;
		КонецЕсли;
							 
	КонецЦикла; 
	
	Движения.ОсновныеНачисления.Записать(, Истина);

КонецПроцедуры
```
