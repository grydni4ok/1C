На форме имеем поле куда вводим адрес, откуда хотим получать данные, и кнопка получить данные.
Обработчик команды, получающей данные, заполним следующим образом
```
&НаКлиенте
Процедура ПолучитьДанные(Команда)
  // Сформировать строку URL.
  АдресРесурса = СокрЛП(Адрес);
  
  // Отправить запрос на сервер.
  ВыполнитьЗапрос("GET", АдресРесурса, ""); 

КонецПроцедуры
```
В этом обработчике в переменной АдресРесурса мы запоминаем значение реквизита обработки Адрес, в котором хранится URL для обращения к HTTP-сервису.
Затем вызываем процедуру ВыполнитьЗапрос() и передаем в нее в качестве параметров адрес ресурса и имя метода ("GET"), котoрый надо выполнить на сервере.
```
Процедура ВыполнитьЗапрос(ИмяМетода, АдресРесурса, СтрокаТелаЗапроса)
	
	Сообщение = Новый СообщениеПользователю;
	// Установить имя сервера.
	СерверИсточник = "localhost";

	Попытка
		// Создать HTTP-соединение с сервером localhost.
		HTTPСоединение = Новый HTTPСоединение(СерверИсточник);
	Исключение
		// Вывести сообщение об ошибке соединения с сервером.
		Сообщение.Текст = "Не удалось соединиться с сервером: " + СерверИсточник;
		Сообщение.Сообщить();
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;	

	// Создать HTTP-запрос на основе URL.
	HTTPЗапрос = Новый HTTPЗапрос(АдресРесурса);
	
	// Установить тело запроса из строки JSON.
	Если ИмяМетода <> "DELETE" И ИмяМетода <> "GET" Тогда
		HTTPЗапрос.УстановитьТелоИзСтроки(СтрокаТелаЗапроса);
	КонецЕсли;
	
	Попытка
		// Получить ответ сервера в виде объекта HTTPОтвет.
		Результат = HTTPСоединение.ВызватьHTTPМетод(ИмяМетода, HTTPЗапрос);
		// Получить содержимое ответа сервера в виде строки.
		Сообщение.Текст = Результат.ПолучитьТелоКакСтроку();
		Сообщение.Сообщить();
	Исключение
		// Вывести сообщение об ошибке при получении ответа сервера.
		Сообщение.Текст = ОписаниеОшибки();
		Сообщение.Сообщить();
		Возврат;
	КонецПопытки;	
	
КонецПроцедуры
```
