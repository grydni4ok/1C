Более подробно HTTP-сервисы описаны в докуменации «1С:Предприятия 8» в разделе «Глава 17. Механизмы интернет-сервисов – HTTP-сервисы – HTTP-сервисы». 

В платформе поддерживается работа с двумя видами веб-сервисов:
  1) HTTP-сервисы, реализованные в технологии REST (хорошо подходят для мобильных приложений)
  2) Web-сервисы, работающие по протоколу SOAP

Для обращения к HTTP-сервису нужно сформировать URL, который в общем виде выглядит следующим образом: http://host/base/hs/<корневойURL>/<относительныйURL>.
  - http://host/base – это обычный URL, по которому выполняется доступ, например, к информационной базе с помощью веб-клиента. Необходимо помнить, что при использовании информационной базы, в которой настроено разделение данных, значения разделителей можно указывать только в URL информационной базы. Указание разделителей с помощью параметра Z не поддерживается;
  - hs – признак того, что выполняется обращение к HTTP-сервису (в отличие от ws, который определяет доступ к Web-сервису);
  - <корневойURL> – имя ресурса, которое определяет группу ресурсов, связанных общим смыслом. Задается в свойстве КорневойURL объекта HTTP-сервис;
  - <относительныйURL> – определяет ресурс, к которому будет выполняться обращение. Относительный URL, указанный в запросе, будет использован для определения конкретного ресурса, к которому выполняется обращение. Правило сопоставления задается в свойстве Шаблон объекта Шаблон URL, подчиненного объекту HTTP-сервис.

К примеру мы создали HTTP сервис, и создали внутри его шаблон /code/{Код}. Мы хотим по коду, получать данные о сотруднике. Опубликовав его и обращаясь http://localhost/Test/hs/worker/code/000000001 мы хотим получить следующую информацию
```
{
  "Алексеев Сергей Иванович": {
    "Код": "000000001",
    "Должность": "Администратор"
  }
}
```
Для этого мы пишем функцию со следующим кодом:
```
	Ответ = Новый HTTPСервисОтвет(200);
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();  
	
	Код = Запрос.ПараметрыURL.Получить("Код");
	Если Код = Неопределено Тогда
		Ответ = Новый HTTPСервисОтвет(400);
		Ответ.УстановитьТелоИзСтроки("Не задан код");
		Ответ.Заголовки.Вставить("Content-type", "application/json");
		Возврат Ответ;
	КонецЕсли;
	
	Выборка = Справочники.Сотрудники.НайтиПоКоду(Код);
	Если Выборка = Справочники.Сотрудники.ПустаяСсылка() Тогда 	
		Ответ = Новый HTTPСервисОтвет(404);
		Ответ.УстановитьТелоИзСтроки("Не найден данный сотрудник по коду");
		Ответ.Заголовки.Вставить("Content-type", "application/json"); 
		Возврат Ответ;
	КонецЕсли; 
	
	Запись.ЗаписатьНачалоОбъекта();
	
	Запись.ЗаписатьИмяСвойства(Выборка.Наименование);
	Запись.ЗаписатьНачалоОбъекта();
	
	Запись.ЗаписатьИмяСвойства("Код");
	Запись.ЗаписатьЗначение(Выборка.Код);
	Запись.ЗаписатьИмяСвойства("Должность");
	Запись.ЗаписатьЗначение(Строка(Выборка.Должность)); 
	
	Запись.ЗаписатьКонецОбъекта();
	
	Запись.ЗаписатьКонецОбъекта();
	Результат = Запись.Закрыть();
	
	Ответ.УстановитьТелоИзСтроки(Результат);
	Ответ.Заголовки.Вставить("Content-type", "application/json");
	Возврат Ответ;
```
В этом коде мы считываем код, который передался нам из браузерной ссылки, в данном случае это код 000000001. Проверяем чтобы значение код не было пустым. Затем пытаемся найти сотрудника по коду, если ни один сотрудник не был найден, сообщаем об этом. Затем если сотрудник был найден, создаем JSON строку, и записываем ее. После чего возвращаем значение.
